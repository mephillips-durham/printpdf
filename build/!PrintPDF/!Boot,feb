| >!Boot
|
| Boot file for PrintPDF
|
| (c) Stephen Fryatt, 2005

Set PrintPDF$Help <Obey$Dir>.!Help
Set PrintPDF$Version "0.10"
Set PrintPDF$Web "http://www.stevefryatt.org.uk/software/printpdf/"
Set PrintPDF$Title "PrintPDF"
Set PrintPDF$Publisher "Stephen Fryatt"
Set PrintPDF$Description "PostScript to PDF Conversion"

If "<PrintPDF$Dir>"="" Then Set PrintPDF$Dir <Obey$Dir>

| Set the filetype information.

Set File$Type_1D8 PrintPDF

| Claim PrintPDF files if these have not been claimed already.

If "<Alias$@RunType_1D8>" = "" Then Set Alias$@RunType_1D8 Run <PrintPDF$Dir>.!Run -file %%*0

| Set the application sprites.

Set PrintPDF$Sprites <PrintPDF$Dir>.!Sprites
If "<Boot$OSVersion>" >= "400" Then Set PrintPDF$Sprites <PrintPDF$Dir>.4Sprites
If "<Boot$OSVersion>" >= "500" Then Set PrintPDF$Sprites <PrintPDF$Dir>.5Sprites
If "<Boot$OSVersion>" >= "600" Then Set PrintPDF$Sprites <PrintPDF$Dir>.4Sprites

IconSprites <PrintPDF$Sprites>

Unset PrintPDF$Sprites
